 <h2> Title: Easy way to print Perl array? (with a little formatting) </h2> <h3> funk-shun, question_id: 5741101 </h3>Score: 108, Tags: {perl} <br><p>Is there an <strong>easy</strong> way to print out a Perl array with commas in between each element?</p>

<p>Writing a for loop to do it is pretty easy but not quite elegant....if that makes sense.</p>
------------------------------------------------------------------ <br><h3> Answer 5741144 Alex: </h3><p>Just use <a href="http://perldoc.perl.org/functions/join.html" rel="noreferrer"><code>join()</code></a>:</p>

<pre><code># assuming @array is your array:
print join(", ", @array);
</code></pre>
<h4> Comment 6574581 Oesor: </h4>There&#39;s more than one way to do it.. but this is the way that doesn&#39;t make the person maintaining the code after you hate you. Yes, perldoc perlvar exists, but I&#39;d rather glance over &quot;join &#39;, &#39; @array&quot; than hit up perlvar every other line to figure out what all the esoteric variables are doing.<br><h4> Comment 6568924 musiKk: </h4>Or, if you want to be really dirty: <code>{local $,=&#39;, &#39;;print @array}</code>.<br><h4> Comment 22502002 Randall: </h4>This worked really nicely for printing the directory contents.    <code>print &quot;current directory contains &quot; . join(&#39;, &#39;, &lt;*&gt;) . &quot;\n&quot;;</code><br><h4> Comment 6580379 musiKk: </h4>@Oesor: That&#39;s why I called it dirty. But well, looking at the other answers, I&#39;m not really the worst offender. :)<br><h4> Comment 6580421 Alex: </h4>@musiKK I like it! It&#39;s something I&#39;d use if I wanted to puzzle my fellow co workers. :)<br>------------------------------------------------------------------ <br><h3> Answer 5741454 Eugene Yarmash: </h3><p>You can use <a href="http://search.cpan.org/perldoc?Data::Dump" rel="noreferrer"><code>Data::Dump</code></a>:</p>

<pre><code>use Data::Dump qw(dump);
my @a = (1, [2, 3], {4 =&gt; 5});
dump(@a);
</code></pre>

<p>Produces:</p>

<pre><code>"(1, [2, 3], { 4 =&gt; 5 })"
</code></pre>
<h4> Comment 23165589 MUY Belgium: </h4>On Ubuntu/Mint, install the libdata-dump-perl package.<br><h4> Comment 48597727 L&#233;o L&#233;opold Hertz 준영: </h4>Best answer here! Works with multidimensional arrays too. Most flexible.<br>------------------------------------------------------------------ <br><h3> Answer 5741683 DavidO: </h3><p>If you're coding for the kind of clarity that would be understood by someone who is just starting out with Perl, the traditional this construct says what it means, with a high degree of clarity and legibility:</p>

<pre><code>$string = join ', ', @array;
print "$string\n";
</code></pre>

<p>This construct is documented in <code>perldoc -f</code><a href="http://perldoc.perl.org/functions/join.html" rel="nofollow noreferrer"><code>join</code></a>.</p>

<p>However, I've always liked how simple <code>$,</code> makes it.  The special variable  <code>$"</code> is for interpolation, and the special variable <code>$,</code> is for lists.  Combine either one with dynamic scope-constraining '<code>local</code>' to avoid having ripple effects throughout the script:</p>

<pre><code>use 5.012_002;
use strict;
use warnings;

my @array = qw/ 1 2 3 4 5 /;

{
    local $" = ', ';
    print "@array\n"; # Interpolation.
}
</code></pre>

<p>OR with $,:</p>

<pre><code>use feature q(say);
use strict;
use warnings;

my @array = qw/ 1 2 3 4 5 /;
{
    local $, = ', ';
    say @array; # List
}
</code></pre>

<p>The special variables <code>$,</code> and <code>$"</code> are documented in <a href="http://perldoc.perl.org/perlvar.html" rel="nofollow noreferrer">perlvar</a>. The <code>local</code> keyword, and how it can be used to constrain the effects of altering a global punctuation variable's value is probably best described in <a href="http://perldoc.perl.org/perlsub.html#Temporary-Values-via-local()" rel="nofollow noreferrer">perlsub</a>.</p>

<p>Enjoy!</p>
------------------------------------------------------------------ <br><h3> Answer 5741208 Andreas: </h3><p>Also, you may want to try <a href="http://perldoc.perl.org/Data/Dumper.html" rel="nofollow noreferrer">Data::Dumper</a>. Example:</p>

<pre class="lang-perl prettyprint-override"><code>use Data::Dumper;

# simple procedural interface
print Dumper($foo, $bar);
</code></pre>
<h4> Comment 6573780 shawnhcorey: </h4>Data::Dumper is a standard module and is installed along with Perl.  To get a list of all the standard pragmatics and modules, see <code>pelrdoc perlmodlib</code>.<br>------------------------------------------------------------------ <br><h3> Answer 17172608 Eugene Yarmash: </h3><p>For inspection/debugging check the <a href="http://search.cpan.org/perldoc?Data%3a%3aPrinter"><code>Data::Printer</code></a> module. It is meant to do one thing and one thing only:</p>

<blockquote>
  <p>display Perl variables and objects on screen, properly formatted (to
  be inspected by a human)</p>
</blockquote>

<p>Example usage:</p>

<pre><code>use Data::Printer;  
p @array;  # no need to pass references
</code></pre>

<p>The code above might output something like this (with colors!):</p>

<pre><code>   [
       [0] "a",
       [1] "b",
       [2] undef,
       [3] "c",
   ]
</code></pre>
<h4> Comment 95910496 planetp: </h4>Under Ubuntu one can install the <code>libdata-printer-perl</code> package.<br>------------------------------------------------------------------ <br><h3> Answer 5741230 Yi Zhao: </h3><p>You can simply <code>print</code> it.</p>

<pre><code>@a = qw(abc def hij);

print "@a";
</code></pre>

<p>You will got:</p>

<pre><code>abc def hij
</code></pre>
<h4> Comment 6569408 Toto: </h4>You have commas in the print because they are in each element of the array. You really should declare <code>use warnings;</code> and see what it says.<br>------------------------------------------------------------------ <br><h3> Answer 5747886 gleeco: </h3><pre><code># better than Dumper --you're ready for the WWW....

use JSON::XS;
print encode_json \@some_array
</code></pre>
------------------------------------------------------------------ <br><h3> Answer 36221190 TVNshack: </h3><p>Using <code>Data::Dumper</code> :</p>

<pre><code>use strict;
use Data::Dumper;

my $GRANTstr = 'SELECT, INSERT, UPDATE, DELETE, LOCK TABLES, EXECUTE, TRIGGER';
$GRANTstr    =~ s/, /,/g;
my @GRANTs   = split /,/ , $GRANTstr;

print Dumper(@GRANTs) . "===\n\n";

print Dumper(\@GRANTs) . "===\n\n";

print Data::Dumper-&gt;Dump([\@GRANTs], [qw(GRANTs)]);
</code></pre>

<p>Generates three different output styles:</p>

<pre><code>$VAR1 = 'SELECT';
$VAR2 = 'INSERT';
$VAR3 = 'UPDATE';
$VAR4 = 'DELETE';
$VAR5 = 'LOCK TABLES';
$VAR6 = 'EXECUTE';
$VAR7 = 'TRIGGER';
===

$VAR1 = [
          'SELECT',
          'INSERT',
          'UPDATE',
          'DELETE',
          'LOCK TABLES',
          'EXECUTE',
          'TRIGGER'
        ];
===

$GRANTs = [
            'SELECT',
            'INSERT',
            'UPDATE',
            'DELETE',
            'LOCK TABLES',
            'EXECUTE',
            'TRIGGER'
          ];
</code></pre>
------------------------------------------------------------------ <br><h3> Answer 38532994 009: </h3><p>This might not be what you're looking for, but here's something I did for an assignment:</p>

<pre><code>$" = ", ";
print "@ArrayName\n";
</code></pre>
<h4> Comment 125449240 Roman: </h4>Where is this $&quot; variable documented? Is there a name for it? Edit: found it here: <a href="https://perldoc.perl.org/variables" rel="nofollow noreferrer">perldoc.perl.org/variables</a><br>------------------------------------------------------------------ <br><h3> Answer 44696390 PodTech.io: </h3><p>Map can also be used, but sometimes hard to read when you have lots of things going on.</p>

<pre><code>map{ print "element $_\n" }   @array; 
</code></pre>
------------------------------------------------------------------ <br><h3> Answer 46987532 K.Minoda: </h3><p>I've not tried to run below, though. I think this's a tricky way.</p>

<pre><code>map{print $_;} @array;
</code></pre>
